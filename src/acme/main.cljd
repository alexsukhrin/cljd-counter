(ns acme.main
  (:require ["package:flutter/material.dart" :as m]
            [cljd.flutter :as f]))

(def counter (atom 0))

(defn main []
  (f/run
   (m/MaterialApp
    .title "Flutter Demo"
    .theme
    (m/ThemeData
     .colorScheme
     (m.ColorScheme/fromSeed .seedColor m.Colors/deepPurple)
     .useMaterial3 true)
    .home
    (f/widget
     :watch [N counter]
     :get {{{:flds [inversePrimary]} .-colorScheme} m/Theme}
     (m/Scaffold
      .appBar (m/AppBar
               .backgroundColor inversePrimary
               .title (m/Text "Flutter Demo Home Page"))
      .body (m/Center
             .child (m/Column .mainAxisAlignment m/MainAxisAlignment.center
                              .children [(m/Text "You have pushed the button this many times:")
                                         (f/widget 
                                          :get {{{:flds [headlineMedium]} .-textTheme} m/Theme}
                                          (m/Text (str N) .style headlineMedium))]))
      .floatingActionButton (m/FloatingActionButton 
                             .onPressed #(swap! counter inc)
                             .tooltip "Increment"
                             .child (m/Icon m/Icons.add)))))))
